# Maintainer: Mattias Andr√©e <`base64 -d`(bWFhbmRyZWUK)@member.fsf.org>

pkgname=dooble
pkgver=1.46
pkgrel=3
pkgdesc='A safe WebKit Web browser'
url='http://dooble.sourceforge.net/'
arch=('i686' 'x86_64')
license=('GPL2')
depends=('libpng' 'qt5-base' 'qt5-webkit' 'openssl')
makedepends=('libpng' 'qt5-base' 'qt5-webkit' 'openssl' 'qt5-tools')
source=("http://downloads.sourceforge.net/project/dooble/Version%20${pkgver}/Dooble.d.tar.gz")
sha256sums=(50ab11855091bc00a7d41e3b17ef49354ef729e1cd21694c958100398ba6e410)
options=('!emptydirs')

# Dependency (from ldd output) tree:
# libpng
# qt5-base
#   icu
#     gcc-libs
#   libjpeg-turbo
#   sqlite
#   mesa-libgl
#     mesa
#       libdrm
#       libxdamage
#       libxxf86vm
#   libxrender
# qt5-webkit
#   libxcomposite
#     libxfixes
#       libx11
#         libxcb
#           libxdmcp & libxau
#             glibc
#       libxext
#   libxslt
#     libgcrypt
#       libgpg-error
#     libxml2
#       xz
#       zlib
#   qt5-sensors & qt5-location
#     qt5-declarative
#   gstreamer0.10-base
#     gstreamer0.10
#       glib2
#         libffi
#         pcre
#     orc
# openssl



build()
{
    cd "$srcdir/dooble.d/trunk/browser/"
    sed -i 's_-Werror__g' dooble.qt5.pro
    qmake-qt5 -o Makefile dooble.qt5.pro
    make distclean
    qmake-qt5 -o Makefile dooble.qt5.pro
    sed -i '/^INCPATH/s:=:= -I/usr/include/qt/QtWidgets:' Makefile
    sed -i '/^INCPATH/s:=:= -I/usr/include/qt/QtWebKitWidgets:' Makefile
    sed -i '/#include/s:QtGui/\([^>]*\)QAction:\1:' Include/*.h
    make
}

package()
{
    cd "$srcdir/dooble.d/trunk/browser/"
    make INSTALL_ROOT="$pkgdir" install
    
    msg "Fixing file structure"
    mkdir "${pkgdir}/opt"
    mv "${pkgdir}/usr/local/dooble" "${pkgdir}/opt/${pkgname}"
    mv "${pkgdir}/usr/local/include" "${pkgdir}/opt/${pkgname}"
    mv "${pkgdir}/usr/share/"* "${pkgdir}/opt/${pkgname}"
    rmdir "${pkgdir}/usr/local/"
    rmdir "${pkgdir}/usr/share/"
    rmdir "${pkgdir}/usr/"
    
    msg "Editing files for the file structure modification"
    sed -i s_"/usr/local/dooble"_"/opt/${pkgname}"_g   \
	"${pkgdir}/opt/${pkgname}/dooble.sh"           \
	"${pkgdir}/opt/${pkgname}/applications/dooble.desktop"
    sed -i s_'/usr/local/dooble/Lib\x00'_'/opt/dooble/Lib\x00\x00\x00\x00\x00\x00\x00'_g \
	"${pkgdir}/opt/${pkgname}/Dooble" # The replacements's length must match the pattern's length
    sed -i s_'\.sh$'__g \
	"${pkgdir}/opt/${pkgname}/applications/dooble.desktop"
    
    msg "Renaming files"
    mv "${pkgdir}/opt/${pkgname}/dooble.sh" \
       "${pkgdir}/opt/${pkgname}/dooble"
    if [ ! "${pkgname}" = "dooble" ]; then
        mv "${pkgdir}/opt/${pkgname}/applications/dooble.desktop" \
           "${pkgdir}/opt/${pkgname}/applications/${pkgname}.desktop"
	mv "${pkgdir}/opt/${pkgname}/icons/hicolor/48x48/dooble.png" \
	   "${pkgdir}/opt/${pkgname}/icons/hicolor/48x48/${pkgname}.png"
    fi
    
    msg "Editing files for the renaming of files"
    sed -i s_"/usr/share/"_"/opt/${pkgname}/"_g \
	"${pkgdir}/opt/${pkgname}/applications/${pkgname}.desktop"
    if [ ! "${pkgname}" = "dooble" ]; then
	sed -i s_"dooble.png"_"${pkgname}.png"_g \
	    "${pkgdir}/opt/${pkgname}/applications/${pkgname}.desktop"
    fi
    
    msg "Add missing files"
    mkdir -p "$pkgdir/opt/${pkgname}/Lib"
    install -m755 "./libSpotOn/libspoton.so" "$pkgdir/opt/${pkgname}/Lib"
}

