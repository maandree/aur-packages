# Maintainer: Mattias Andr√©e <`base64 -d`(bWFhbmRyZWUK)@member.fsf.org>
pkgname=dooble
pkgver=1.42_dev
pkgrel=1
_r=r5278
pkgdesc='A safe WebKit Web browser'
url='http://dooble.sourceforge.net/'
arch=('i386' 'x86_64')
license=('GPL2')
depends=('bzip2' 'fontconfig' 'gstreamer0.10-base' 'libgcrypt' 'libjpeg'
         'libsm' 'libxrender' 'mesa-libgl' 'openssl' 'qtwebkit' 'udev' 'xz')
# Transversally included dependencies:  glibc expat freetype2 pcre libffi glib2 libxml2 orc gstreamer0.10 zlib util-linux-ng
#                                       libice libxcb libx11 libxfixes libxext libxdamage libdrm mesa libpng sqlite gcc-libs
#                                       libxau libxdmcp libxxf86vm qt4 libgpg-error
makedepends=('svn')
options=('!emptydirs')


build()
{
    cd "$srcdir"
    svn co -r "${_r}" "https://dooble.svn.sourceforge.net/svnroot/dooble" dooble
    cd "dooble/trunk/browser/"
    sed -i 's_-Werror__g' dooble.pro
    qmake-qt4 -o Makefile dooble.pro
    make
}

package()
{
    cd "$srcdir/dooble/trunk/browser/"
    make INSTALL_ROOT="$pkgdir" install
    
    msg "Fixing file structure"
    mkdir "$pkgdir/opt"
    mv "$pkgdir/usr/local/dooble" "$pkgdir/opt"
    mv "$pkgdir/usr/local/include" "$pkgdir/usr"
    rmdir "$pkgdir/usr/local/" || echo "Cannot remove \$pkgdir/usr/local/"
    
    msg "Editing files for the file structure modification"
    sed -i s_'/usr/local/dooble'_'/opt/dooble'_g "$pkgdir/opt/dooble/dooble.sh"
    sed -i s_'/usr/local/dooble/Lib\x00'_'/opt/dooble/Lib\x00\x00\x00\x00\x00\x00\x00'_g "$pkgdir/opt/dooble/Dooble"
    
    msg "Renaming \$pkgdir/opt/dooble/dooble.sh to \$pkgdir/opt/dooble/dooble"
    mv "$pkgdir/opt/dooble/dooble.sh" "$pkgdir/opt/dooble/dooble"
    
    # XXX: This will have been resolved in 1.42 (without -dev)
    if [ ! -f "$pkgdir/opt/dooble/Lib/libspoton.so" ]; then
        msg "Installing files missed by the installer"
        mkdir -p "$pkgdir/opt/dooble/Lib"
        cp "$srcdir/dooble/trunk/browser/LibSpotOn/libspoton.so" "$pkgdir/opt/dooble/Lib"
    else
        msg "Notify maintainer about obsolete installation instruction."
    fi
}

