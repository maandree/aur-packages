# Maintainer: Mattias Andr√©e <`base64 -d`(bWFhbmRyZWUK)@member.fsf.org>

pkgname=dooble-qt4
pkgver=1.47
pkgrel=1
pkgdesc='A safe WebKit Web browser'
url='http://dooble.sourceforge.net/'
arch=('i686' 'x86_64')
license=('GPL2')
depends=('qtwebkit')
makedepends=('qtwebkit')
source=("http://downloads.sourceforge.net/project/dooble/Version%20${pkgver}/Dooble.d.tar.gz")
sha256sums=('1840074b5648bd3755f7cefa65388e562c8d7159c119240e5c5efe0f7e1316b8')
options=('!emptydirs')

# Dependency (from ldd output) tree:
# qtwebkit
#   qt4
#     openssl (via ca-certificates)
#     libsm (via libxt via libxmu via xorg-xset via xdg-utils)
#       libice
#       libutil-linux (via util-linux)
#     libjpeg (via libtiff)
#     libxrender (via libxrandr)
#     sqlite
#     fontconfig
#       expat
#         glibc
#       freetype2
#         bzip2
#         zlib
#         sh
#         libpng
#         harfbuzz
#           glib2
#             pcre
#             libffi
#           graphite
#             gcc-libs
#   gstreamer0.10-base
#     gstreamer0.10
#       libxml2
#         xz
#     orc
#   mesa-libgl (as libgl)
#     mesa
#       libdrm
#       libxxf86vm
#         libxext
#       libxdamage
#         libxfixes
#           libx11
#             libxcb
#               libxdmcp
#               libxau
#       libxshmfence
#       libsystemd (via systemd)
#         libgcrypt
#           libgpg-error


build()
{
    cd "$srcdir/dooble.d/trunk/browser/"
    sed -i 's_-Werror__g' dooble.pro
    qmake-qt4 -o Makefile dooble.pro
    make distclean
    qmake-qt4 -o Makefile dooble.pro
    make
}

package()
{
    cd "$srcdir/dooble.d/trunk/browser/"
    make INSTALL_ROOT="$pkgdir" install
    
    msg "Fixing file structure"
    mkdir "${pkgdir}/opt"
    mv "${pkgdir}/usr/local/dooble" "${pkgdir}/opt/${pkgname}"
    mv "${pkgdir}/usr/local/include" "${pkgdir}/opt/${pkgname}"
    mv "${pkgdir}/usr/share/"* "${pkgdir}/opt/${pkgname}"
    rmdir "${pkgdir}/usr/local/"
    rmdir "${pkgdir}/usr/share/"
    rmdir "${pkgdir}/usr/"
    
    msg "Editing files for the file structure modification"
    sed -i s_"/usr/local/dooble"_"/opt/${pkgname}"_g   \
	"${pkgdir}/opt/${pkgname}/dooble.sh"           \
	"${pkgdir}/opt/${pkgname}/applications/dooble.desktop"
    sed -i s_'/usr/local/dooble/Lib\x00'_'/opt/dooble-qt4/Lib\x00\x00\x00'_g \
	"${pkgdir}/opt/${pkgname}/Dooble" # The replacements's length must match the pattern's length
    sed -i s_'\.sh$'__g \
	"${pkgdir}/opt/${pkgname}/applications/dooble.desktop"
    
    msg "Renaming files"
    mv "${pkgdir}/opt/${pkgname}/dooble.sh" \
       "${pkgdir}/opt/${pkgname}/dooble"
    if [ ! "${pkgname}" = "dooble" ]; then
        mv "${pkgdir}/opt/${pkgname}/applications/dooble.desktop" \
           "${pkgdir}/opt/${pkgname}/applications/${pkgname}.desktop"
	mv "${pkgdir}/opt/${pkgname}/icons/hicolor/48x48/dooble.png" \
	   "${pkgdir}/opt/${pkgname}/icons/hicolor/48x48/${pkgname}.png"
    fi
    
    msg "Editing files for the renaming of files"
    sed -i s_"/usr/share/"_"/opt/${pkgname}/"_g \
	"${pkgdir}/opt/${pkgname}/applications/${pkgname}.desktop"
    if [ ! "${pkgname}" = "dooble" ]; then
	sed -i s_"dooble.png"_"${pkgname}.png"_g \
	    "${pkgdir}/opt/${pkgname}/applications/${pkgname}.desktop"
    fi
    
    msg "Add missing files"
    mkdir -p "$pkgdir/opt/${pkgname}/Lib"
    install -m755 "./libSpotOn/libspoton.so" "$pkgdir/opt/${pkgname}/Lib"
}

