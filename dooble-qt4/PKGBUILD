# Maintainer: Mattias Andr√©e <`base64 -d`(bWFhbmRyZWUK)@member.fsf.org>

pkgname=dooble-qt4
pkgver=1.46
pkgrel=1
pkgdesc='A safe WebKit Web browser'
url='http://dooble.sourceforge.net/'
arch=('i386' 'x86_64')
license=('GPL2')
depends=('qtwebkit')
makedepends=('qtwebkit')
source=("http://downloads.sourceforge.net/project/dooble/Version%20${pkgver}/Dooble.d.tar.gz")
sha256sums=(50ab11855091bc00a7d41e3b17ef49354ef729e1cd21694c958100398ba6e410)
options=('!emptydirs')

# Dependency (from ldd output) tree:
# qtwebkit
#   qt4
#     openssl (via ca-certificates)
#     libsm (via libxt via libxmu via xorg-xset via xdg-utils)
#       libice
#       libutil-linux (via util-linux)
#     libjpeg (via libtiff)
#     libxrender (via libxrandr)
#     sqlite
#     fontconfig
#       expat
#         glibc
#       freetype2
#         bzip2
#         zlib
#         sh
#         libpng
#         harfbuzz
#           glib2
#             pcre
#             libffi
#           graphite
#             gcc-libs
#   gstreamer0.10-base
#     gstreamer0.10
#       libxml2
#         xz
#     orc
#   mesa-libgl (as libgl)
#     mesa
#       libdrm
#       libxxf86vm
#         libxext
#       libxdamage
#         libxfixes
#           libx11
#             libxcb
#               libxdmcp
#               libxau
#       libxshmfence
#       libsystemd (via systemd)
#         libgcrypt
#           libgpg-error


build()
{
    cd "$srcdir/dooble.d/trunk/browser/"
    sed -i 's_-Werror__g' dooble.pro
    qmake-qt4 -o Makefile dooble.pro
    make distclean
    qmake-qt4 -o Makefile dooble.pro
    make
}

package()
{
    cd "$srcdir/dooble.d/trunk/browser/"
    make INSTALL_ROOT="$pkgdir" install
    
    msg "Fixing file structure"
    mkdir "$pkgdir/opt"
    mv "$pkgdir/usr/local/dooble" "$pkgdir/opt/dooble-qt4"
    mv "$pkgdir/usr/local/include" "$pkgdir/usr"
    rmdir "$pkgdir/usr/local/" || echo "Cannot remove \$pkgdir/usr/local/"
    
    msg "Editing files for the file structure modification"
    sed -i s_'/usr/local/dooble'_'/opt/dooble-qt4'_g "$pkgdir/opt/dooble-qt4/dooble.sh" "$pkgdir/usr/share/applications/dooble.desktop"
    sed -i s_'/usr/local/dooble/Lib\x00'_'/opt/dooble-qt4/Lib\x00\x00\x00'_g "$pkgdir/opt/dooble-qt4/Dooble"
    
    msg "Renaming \$pkgdir/opt/dooble-qt4/dooble.sh to \$pkgdir/opt/dooble-qt4/dooble"
    mv "$pkgdir/opt/dooble-qt4/dooble.sh" "$pkgdir/opt/dooble-qt4/dooble"
    
    msg "Add missing files"
    mkdir -p "$pkgdir/opt/dooble-qt4/Lib"
    install -m755 "./libSpotOn/libspoton.so" "$pkgdir/opt/dooble-qt4/Lib"

    msg "Renaming and modifying files for compatibility with the qt5 version (dooble) of this package"
    mv "$pkgdir/usr/share/applications"/dooble{,-qt4}.desktop
    mv "$pkgdir/usr/share/icons/hicolor/48x48"/dooble{,-qt4}.png
    sed -i s_'dooble.png'_'dooble-qt4.png'_g "$pkgdir/usr/share/applications/dooble-qt4.desktop"
    sed -i s_'^Name\(*\)$'_'Name\1 (Qt 4)'_g "$pkgdir/usr/share/applications/dooble-qt4.desktop"
    mv "$pkgdir/usr/include"/dooble{,-qt4}
}

