# Maintainer: Mattias Andr√©e <`base64 -d`(bWFhbmRyZWUK)@member.fsf.org>

pkgname=dooble-qt4
pkgver=1.47
pkgrel=4
pkgdesc='A safe WebKit Web browser'
url='http://dooble.sourceforge.net/'
arch=('i686' 'x86_64')
license=('custom:3-clause BSD')
depends=('qtwebkit' 'libspoton=0.09.04' "dooble-common=${pkgver}")
makedepends=('qtwebkit' 'sed' 'coreutils')
source=("http://downloads.sourceforge.net/project/dooble/Version%20${pkgver}/Dooble.d.tar.gz")
sha256sums=('1840074b5648bd3755f7cefa65388e562c8d7159c119240e5c5efe0f7e1316b8')
options=('!emptydirs')

# Dependency (from ldd output) tree:
# qtwebkit
#   qt4
#     openssl (via ca-certificates)
#     libsm (via libxt via libxmu via xorg-xset via xdg-utils)
#       libice
#       libutil-linux (via util-linux)
#     libjpeg (via libtiff)
#     libxrender (via libxrandr)
#     sqlite
#     fontconfig
#       expat
#         glibc
#       freetype2
#         bzip2
#         zlib
#         sh
#         libpng
#         harfbuzz
#           glib2
#             pcre
#             libffi
#           graphite
#             gcc-libs
#   gstreamer0.10-base
#     gstreamer0.10
#       libxml2
#         xz
#     orc
#   mesa-libgl (as libgl)
#     mesa
#       libdrm
#       libxxf86vm
#         libxext
#       libxdamage
#         libxfixes
#           libx11
#             libxcb
#               libxdmcp
#               libxau
#       libxshmfence
#       libsystemd (via systemd)
#         libgcrypt
#           libgpg-error


build()
{
    cd "$srcdir/dooble.d/trunk/browser/"
    sed -i 's_-Werror__g' dooble.pro
    qmake-qt4 -o Makefile dooble.pro
    make distclean
    qmake-qt4 -o Makefile dooble.pro
    make
}

package()
{
    cd "$srcdir/dooble.d/trunk/browser/"
    make INSTALL_ROOT="$pkgdir" install
    
    msg "Fixing file structure"
    mkdir -- "${pkgdir}/opt"
    mv -- "${pkgdir}/usr/local/dooble" "${pkgdir}/opt/${pkgname}"
    mv -- "${pkgdir}/usr/share/"* "${pkgdir}/opt/${pkgname}"
    
    msg "Editing files for the file structure modification"
    sed -i s_"/usr/local/dooble"_"/opt/${pkgname}"_g  \
	-- "${pkgdir}/opt/${pkgname}/dooble.sh"       \
	   "${pkgdir}/opt/${pkgname}/applications/dooble.desktop"
    sed -i /LD_LIBRARY_PATH/d \
	-- "${pkgdir}/opt/${pkgname}/dooble.sh"
    sed -i s_'/usr/local/dooble/Lib\x00'_'/usr/lib\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00'_g \
	-- "${pkgdir}/opt/${pkgname}/Dooble" # The replacements's length must match the pattern's length
    sed -i s_'\.sh$'__g \
	-- "${pkgdir}/opt/${pkgname}/applications/dooble.desktop"
    
    msg "Renaming files"
    mv -- "${pkgdir}/opt/${pkgname}/dooble.sh" \
          "${pkgdir}/opt/${pkgname}/dooble"
    if [ ! "${pkgname}" = "dooble" ]; then
        mv -- "${pkgdir}/opt/${pkgname}/applications/dooble.desktop" \
              "${pkgdir}/opt/${pkgname}/applications/${pkgname}.desktop"
    fi
    
    msg "Remove files an replace with those provided by dooble-common"
    for d in Icons Images Plugins Tab; do
	rm -rf -- "${pkgdir}/opt/${pkgname}/${d}"
	ln -sf "../../usr/share/dooble/${d}" -- "${pkgdir}/opt/${pkgname}/${d}"
    done
    rm -r -- "${pkgdir}/opt/${pkgname}/icons"
    rm -r -- "${pkgdir}/usr/local/include"
    rmdir -- "${pkgdir}/usr/local/"
    rmdir -- "${pkgdir}/usr/share/"
    rmdir -- "${pkgdir}/usr/"
}

